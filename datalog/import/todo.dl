// TODO(lb): Move these elsewhere! These were erroneously included in the
// import*.dl files before these were derived from predicates.inc.

.decl block_predecessor(v0:symbol, v1:symbol)
block_predecessors(bb0, bb1) :-
   block_predecessor(e0, e1),
   variable_is_label(e0),
   variable_is_label(e1),
   block_of_label(bb0, e0),
   block_of_label(bb1, e1).

.decl _instr_bb_entry(v0:symbol, v1:symbol)
instr_block(e0, bb) :-
   _instr_bb_entry(e0, e1),
   instr(e0),
   variable_is_label(e1),
   block_of_label(bb, e1).

.decl _phi_instr_pair_label(v0:symbol, v1:number, v2:symbol)
.decl _phi_instr_pair_value(v0:symbol, v1:number, v2:symbol)
phi_instr_pair(v0, v1, [v2, v3]) :-
   _phi_instr_pair_value(v0, v1, v2),
   phi_instr(v0),
   operand(v2),
   _phi_instr_pair_label(v0, v1, v3),
   variable(v3).

.decl _switch_instr_case_label(v0:symbol, v1:number, v2:symbol)
.decl _switch_instr_case_value(v0:symbol, v1:number, v2:symbol)
switch_instr_case(v0, v1, [v2, v3]) :-
   _switch_instr_case_value(v0, v1, v2),
   switch_instr(v0),
   constant(v2),
   _switch_instr_case_label(v0, v1, v3),
   variable(v3).

.decl _landingpad_instr_catch_clause(v0:Instruction, v1:number, v2:Constant)
.decl _landingpad_instr_filter_clause(v0:Instruction, v1:number, v2:Constant)

catch_clause([v2]), landingpad_instr_clause(v0, v1, [v2]) :-
   _landingpad_instr_catch_clause(v0, v1, v2).

filter_clause([v2]), landingpad_instr_clause(v0, v1, [v2]) :-
   _landingpad_instr_filter_clause(v0, v1, v2).

.decl _constant_in_func(v0:Constant, v1:FunctionName)
.decl _variable_in_func(v0:Variable, v1:FunctionName)

constant_in_func(Constant, Function) :-
   _constant_in_func(Constant, FuncName),
   func_name(Function, FuncName).

variable_in_func(Variable, Function) :-
   _variable_in_func(Variable, FuncName),
   func_name(Function, FuncName).