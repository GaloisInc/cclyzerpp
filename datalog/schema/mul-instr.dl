// http://llvm.org/docs/LangRef.html#mul-instr
// keywords: arithmetic; binary; instr

.type MulInstruction <: symbol
.decl mul_instr(instr:MulInstruction)

instr(v) :- mul_instr(v).

.decl mul_instr_first_operand(instr:MulInstruction, op:Operand)
.decl mul_instr_second_operand(instr:MulInstruction, op:Operand)


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘mul‘ instr must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   mul_instr(Instr),
   !mul_instr_first_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   mul_instr(Instr),
   !mul_instr_second_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   mul_instr(Instr),
   instr_returns_type(Instr, Type),
   !integer_type(Type),
   !integer_vector_type(Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instr_returns_type(Instr, Type),
   mul_instr_first_operand(Instr, LeftOp),
   !operand_has_type(LeftOp, Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instr_returns_type(Instr, Type),
   mul_instr_second_operand(Instr, RightOp),
   !operand_has_type(RightOp, Type).
