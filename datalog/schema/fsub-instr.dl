// http://llvm.org/docs/LangRef.html#fsub-instr
// keywords: arithmetic; binary; instr

.type FSubInstruction <: symbol
.decl fsub_instr(instr:FSubInstruction)

instr(v) :- fsub_instr(v).

.decl fsub_instr_first_operand(instr:FSubInstruction, op:Operand)
.decl fsub_instr_second_operand(instr:FSubInstruction, op:Operand)


//-------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘fsub‘ instr must be:
//      (i)  floating point or
//      (ii) vector of floating point values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//-------------------------------------------------------------------

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fsub_instr(Instr),
   !fsub_instr_first_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fsub_instr(Instr),
   !fsub_instr_second_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fsub_instr(Instr),
   instr_returns_type(Instr, Type),
   !fp_type(Type),
   !fp_vector_type(Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instr_returns_type(Instr, Type),
   fsub_instr_first_operand(Instr, LeftOp),
   !operand_has_type(LeftOp, Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instr_returns_type(Instr, Type),
   fsub_instr_second_operand(Instr, RightOp),
   !operand_has_type(RightOp, Type).
