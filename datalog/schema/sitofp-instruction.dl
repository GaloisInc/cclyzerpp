// http://llvm.org/docs/LangRef.html#sitofp-to-instruction
// keywords: conversion; unary; instruction

.type SIToFPInstruction = Instruction
.decl sitofp_instruction(instr:SIToFPInstruction)

instruction(v) :- sitofp_instruction(v).

.decl sitofp_instruction_from_operand(instr:SIToFPInstruction, val:Operand)
.decl sitofp_instruction_to_type(instr:SIToFPInstruction, type:Type)


// Helper predicate

.decl sitofp_instruction_from_type(instr:SIToFPInstruction, type:Type)

sitofp_instruction_from_type(Instr, Type) :-
   sitofp_instruction_from_operand(Instr, Operand),
   operand_has_type(Operand, Type).


//------------------------------------------------------------------------------
// [Constraints]
//
// The ‘sitofp‘ instruction takes:
//  (i)  a value to cast, which must be a scalar or vector integer value,
//  (ii) and a type to cast it to, which must be a floating point type.
//
// If the value to be casted is a vector integer type:
//  (i)  the type must be a vector floating point type,
//  (ii) with the same number of elements.
//------------------------------------------------------------------------------

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   sitofp_instruction(Instr),
   !sitofp_instruction_from_operand(Instr, _).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   sitofp_instruction(Instr),
   !sitofp_instruction_to_type(Instr, _).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   sitofp_instruction(Instr),
   sitofp_instruction_to_type(Instr, Type),
   !instruction_returns_type(Instr, Type).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   sitofp_instruction(Instr),
   sitofp_instruction_from_type(Instr, Type),
   !integer_type(Type),
   !integer_vector_type(Type).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   sitofp_instruction(Instr),
   sitofp_instruction_to_type(Instr, Type),
   !fp_type(Type),
   !fp_vector_type(Type).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   sitofp_instruction(Instr),
   sitofp_instruction_from_type(Instr, FromType),
   sitofp_instruction_to_type(Instr, ToType),
   !vector_compatible_types(FromType, ToType).
