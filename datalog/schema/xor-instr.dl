// http://llvm.org/docs/LangRef.html#xor-instr
// keywords: bitwise; binary; instr

.type XorInstruction <: symbol
.decl xor_instr(instr:XorInstruction)

instr(v) :-
   xor_instr(v).

.decl xor_instr_first_operand(instr:XorInstruction, op:Operand)
.decl xor_instr_second_operand(instr:XorInstruction, op:Operand)


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘xor‘ instr must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   xor_instr(Instr),
   !xor_instr_first_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   xor_instr(Instr),
   !xor_instr_second_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   xor_instr(Instr),
   instr_returns_type(Instr, Type),
   !integer_type(Type),
   !integer_vector_type(Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instr_returns_type(Instr, Type),
   xor_instr_first_operand(Instr, LeftOp),
   !operand_has_type(LeftOp, Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instr_returns_type(Instr, Type),
   xor_instr_second_operand(Instr, RightOp),
   !operand_has_type(RightOp, Type).
