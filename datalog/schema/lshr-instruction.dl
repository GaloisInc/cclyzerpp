// http://llvm.org/docs/LangRef.html#lshr-instruction
// keywords: bitwise; binary; instruction

.type LShrInstruction = Instruction
.decl lshr_instruction(instr:LShrInstruction)

instruction(v) :-
   lshr_instruction(v).

.decl lshr_instruction_first_operand(instr:LShrInstruction, op:Operand)
.decl lshr_instruction_second_operand(instr:LShrInstruction, op:Operand)


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘lshr‘ instruction must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   lshr_instruction(Instr),
   !lshr_instruction_first_operand(Instr, _).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   lshr_instruction(Instr),
   !lshr_instruction_second_operand(Instr, _).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   lshr_instruction(Instr),
   instruction_returns_type(Instr, Type),
   !integer_type(Type),
   !integer_vector_type(Type).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instruction_returns_type(Instr, Type),
   lshr_instruction_first_operand(Instr, LeftOp),
   !operand_has_type(LeftOp, Type).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instruction_returns_type(Instr, Type),
   lshr_instruction_second_operand(Instr, RightOp),
   !operand_has_type(RightOp, Type).
