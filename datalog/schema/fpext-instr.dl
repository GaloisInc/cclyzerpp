// http://llvm.org/docs/LangRef.html#fpext-to-instr
// keywords: conversion; unary; instr

.type FPExtInstruction <: symbol
.decl fpext_instr(instr:FPExtInstruction)

instr(v) :- fpext_instr(v).

.decl fpext_instr_from_operand(instr:FPExtInstruction, val:Operand)
.decl fpext_instr_to_type(instr:FPExtInstruction, type:Type)


// Helper predicate

.decl fpext_instr_from_type(instr:FPExtInstruction, type:Type)

fpext_instr_from_type(Instr, Type) :-
   fpext_instr_from_operand(Instr, Operand),
   operand_has_type(Operand, Type).


//-------------------------------------------------------------------
// [Constraints]
//
// 1) The ‘fpext‘ instr takes:
//      (i)  a floating point value to cast,
//      (ii) and a floating point type to cast it to.
//
// 2) The source type must be smaller than the destination type.
//-------------------------------------------------------------------

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fpext_instr(Instr),
   !fpext_instr_from_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fpext_instr(Instr),
   !fpext_instr_to_type(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fpext_instr(Instr),
   fpext_instr_to_type(Instr, Type),
   !instr_returns_type(Instr, Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fpext_instr(Instr),
   fpext_instr_from_type(Instr, Type),
   !fp_type(Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fpext_instr(Instr),
   fpext_instr_to_type(Instr, Type),
   !fp_type(Type).
