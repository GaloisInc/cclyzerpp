// http://llvm.org/docs/LangRef.html#fdiv-instr
// keywords: arithmetic; binary; instr

.type FDivInstruction <: symbol
.decl fdiv_instr(instr:FDivInstruction)

instr(v) :- fdiv_instr(v).

.decl fdiv_instr_first_operand(instr:FDivInstruction, op:Operand)
.decl fdiv_instr_second_operand(instr:FDivInstruction, op:Operand)


//-------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘fdiv‘ instr must be:
//      (i)  floating point or
//      (ii) vector of floating point values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//-------------------------------------------------------------------

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fdiv_instr(Instr),
   !fdiv_instr_first_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fdiv_instr(Instr),
   !fdiv_instr_second_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fdiv_instr(Instr),
   instr_returns_type(Instr, Type),
   !fp_type(Type),
   !fp_vector_type(Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instr_returns_type(Instr, Type),
   fdiv_instr_first_operand(Instr, LeftOp),
   !operand_has_type(LeftOp, Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instr_returns_type(Instr, Type),
   fdiv_instr_second_operand(Instr, RightOp),
   !operand_has_type(RightOp, Type).
