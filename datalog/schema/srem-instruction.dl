// http://llvm.org/docs/LangRef.html#srem-instruction
// keywords: arithmetic; binary; instruction

.type SRemInstruction = Instruction
.decl srem_instruction(instr:SRemInstruction)

instruction(v) :- srem_instruction(v).

.decl srem_instruction_first_operand(instr:SRemInstruction, op:Operand)
.decl srem_instruction_second_operand(instr:SRemInstruction, op:Operand)


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘srem‘ instruction must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   srem_instruction(Instr),
   !srem_instruction_first_operand(Instr, _).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   srem_instruction(Instr),
   !srem_instruction_second_operand(Instr, _).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   srem_instruction(Instr),
   instruction_returns_type(Instr, Type),
   !integer_type(Type),
   !integer_vector_type(Type).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instruction_returns_type(Instr, Type),
   srem_instruction_first_operand(Instr, LeftOp),
   !operand_has_type(LeftOp, Type).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instruction_returns_type(Instr, Type),
   srem_instruction_second_operand(Instr, RightOp),
   !operand_has_type(RightOp, Type).
