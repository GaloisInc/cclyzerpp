// http://llvm.org/docs/LangRef.html#urem-instr
// keywords: arithmetic; binary; instr

.type URemInstruction = Instruction
.decl urem_instr(instr:URemInstruction)

instr(v) :- urem_instr(v).

.decl urem_instr_first_operand(instr:URemInstruction, op:Operand)
.decl urem_instr_second_operand(instr:URemInstruction, op:Operand)


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘urem‘ instr must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   urem_instr(Instr),
   !urem_instr_first_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   urem_instr(Instr),
   !urem_instr_second_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   urem_instr(Instr),
   instr_returns_type(Instr, Type),
   !integer_type(Type),
   !integer_vector_type(Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instr_returns_type(Instr, Type),
   urem_instr_first_operand(Instr, LeftOp),
   !operand_has_type(LeftOp, Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instr_returns_type(Instr, Type),
   urem_instr_second_operand(Instr, RightOp),
   !operand_has_type(RightOp, Type).
