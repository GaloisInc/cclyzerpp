// http://llvm.org/docs/LangRef.html#or-instr
// keywords: bitwise; binary; instr

.type OrInstruction <: symbol
.decl or_instr(instr:OrInstruction)

instr(v) :-
   or_instr(v).

.decl or_instr_first_operand(instr:OrInstruction, op:Operand)
.decl or_instr_second_operand(instr:OrInstruction, op:Operand)


//------------------------------------------------------------------------------
// [Constraints]
//
// 1) The two arguments to the ‘or‘ instr must be:
//      (i)  integer or
//      (ii) vector of integer values.
//
// 2) Both arguments must have identical types.
//
// 3) The resulting value is the same type as its operands.
//------------------------------------------------------------------------------

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   or_instr(Instr),
   !or_instr_first_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   or_instr(Instr),
   !or_instr_second_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   or_instr(Instr),
   instr_returns_type(Instr, Type),
   !integer_type(Type),
   !integer_vector_type(Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instr_returns_type(Instr, Type),
   or_instr_first_operand(Instr, LeftOp),
   !operand_has_type(LeftOp, Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   instr_returns_type(Instr, Type),
   or_instr_second_operand(Instr, RightOp),
   !operand_has_type(RightOp, Type).
