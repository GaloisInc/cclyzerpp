// http://llvm.org/docs/LangRef.html#fptosi-to-instr
// keywords: conversion; unary; instr

.type FPToSIInstruction <: symbol
.decl fptosi_instr(instr:FPToSIInstruction)

instr(v) :- fptosi_instr(v).

.decl fptosi_instr_from_operand(instr:FPToSIInstruction, val:Operand)
.decl fptosi_instr_to_type(instr:FPToSIInstruction, type:Type)


// Helper predicate

.decl fptosi_instr_from_type(instr:FPToSIInstruction, type:Type)

fptosi_instr_from_type(Instr, Type) :-
   fptosi_instr_from_operand(Instr, Operand),
   operand_has_type(Operand, Type).


//-------------------------------------------------------------------
// [Constraints]
//
// 1) The ‘fptosi‘ instr takes:
//      (i)  a value to cast, which must be a scalar or vector
//           floating point value,
//      (ii) and a type to cast it to, which must be an integer
//           type.
//
// 2) If the value to be casted is a vector floating point type:
//      (i)  the type must be a vector integer type,
//      (ii) with the same number of elements.
//-------------------------------------------------------------------

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fptosi_instr(Instr),
   !fptosi_instr_from_operand(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fptosi_instr(Instr),
   !fptosi_instr_to_type(Instr, _).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fptosi_instr(Instr),
   fptosi_instr_to_type(Instr, Type),
   !instr_returns_type(Instr, Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fptosi_instr(Instr),
   fptosi_instr_from_type(Instr, Type),
   !fp_type(Type),
   !fp_vector_type(Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fptosi_instr(Instr),
   fptosi_instr_to_type(Instr, Type),
   !integer_type(Type),
   !integer_vector_type(Type).

schema_invalid_instr(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   fptosi_instr(Instr),
   fptosi_instr_from_type(Instr, FromType),
   fptosi_instr_to_type(Instr, ToType),
   !vector_compatible_types(FromType, ToType).
