// http://llvm.org/docs/LangRef.html#ptrtoint-to-instruction
// keywords: conversion; unary; instruction

.type PtrToIntInstruction = Instruction
.decl ptrtoint_instruction(instr:PtrToIntInstruction)

instruction(v) :- ptrtoint_instruction(v).

.decl ptrtoint_instruction_from_operand(instr:PtrToIntInstruction, val:Operand)
.decl ptrtoint_instruction_to_type(instr:PtrToIntInstruction, type:Type)


// Helper predicate

.decl ptrtoint_instruction_from_type(instr:PtrToIntInstruction, type:Type)

ptrtoint_instruction_from_type(Instr, Type) :-
   ptrtoint_instruction_from_operand(Instr, Operand),
   operand_has_type(Operand, Type).


//------------------------------------------------------------------------------
// [Constraints]
//
// The ‘ptrtoint‘ instruction takes:
//  (i)  a value to cast, which must be pointer or vector of pointer values,
//  (ii) and a type to cast it to, which must be an integer or a vector of
//       integers type.
//
// If the value to be casted is a vector pointer type:
//  (i)  the type must be a vector integer type,
//  (ii) with the same number of elements.
//------------------------------------------------------------------------------

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   ptrtoint_instruction(Instr),
   !ptrtoint_instruction_from_operand(Instr, _).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   ptrtoint_instruction(Instr),
   !ptrtoint_instruction_to_type(Instr, _).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   ptrtoint_instruction(Instr),
   ptrtoint_instruction_to_type(Instr, Type),
   !instruction_returns_type(Instr, Type).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   ptrtoint_instruction(Instr),
   ptrtoint_instruction_from_type(Instr, Type),
   !pointer_type(Type),
   !pointer_vector_type(Type).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   ptrtoint_instruction(Instr),
   ptrtoint_instruction_to_type(Instr, Type),
   !integer_type(Type),
   !integer_vector_type(Type).

schema_invalid_instruction(Instr, __FILE__, __LINE__) :-
   schema_sanity(),
   ptrtoint_instruction(Instr),
   ptrtoint_instruction_from_type(Instr, FromType),
   ptrtoint_instruction_to_type(Instr, ToType),
   !vector_compatible_types(FromType, ToType).
